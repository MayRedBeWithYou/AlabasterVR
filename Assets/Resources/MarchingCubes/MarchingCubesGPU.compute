#pragma kernel March
#include "Tables.compute"

static const int numThreads = 8;

struct Triangle {
    float3 vertexC;
    float3 vertexA;
    float3 vertexB;
    float3 normC;
    float3 normA;
    float3 normB;
    float3 colorC;
    float3 colorA;
    float3 colorB;
};

AppendStructuredBuffer<Triangle> triangles;
StructuredBuffer<float> points;
StructuredBuffer<float3> colors;
int numPointsPerAxis;
float isoLevel;
float chunkSize;

float3 realPos(uint3 id)
{
    float3 ids = id;
    float res = numPointsPerAxis - 1;
    ids = ids / res;
    return ids * chunkSize;
}

float3 interpolateVerts(float4 v1, float4 v2) {
    float t = (isoLevel - v1.w) / (v2.w - v1.w);
    return v1.xyz + t * (v2.xyz - v1.xyz);
}

float getRatio(float w1, float w2)
{
    return (isoLevel - w1) / (w2 - w1);
}

int indexFromCoord(uint3 id)
{
    return id.z * numPointsPerAxis * numPointsPerAxis + id.y * numPointsPerAxis + id.x;
}

int indexFromCoord(int x, int y, int z) {
    return z * numPointsPerAxis * numPointsPerAxis + y * numPointsPerAxis + x;
}

float3 getNormal(float3 p1, float3 p2, float3 p3)
{
    float3 U = p2 - p1;
    float3 V = p3 - p1;
    return cross(U, V);
}

float4 sdfValAndPos(int x, int y, int z, uint3 id)
{
    float4 retVal;
    retVal.xyz = realPos(id);
    retVal.w = points[indexFromCoord(x, y, z)];
    return retVal;
}

float4 sdfValAndColor(int x, int y, int z, uint3 id)
{
    float4 retVal;
    retVal.xyz = realPos(id);
    retVal.w = points[indexFromCoord(x, y, z)];
    return retVal;
}

float3 getSdfGradient(uint3 id)
{
    float dx = points[indexFromCoord(uint3(id.x - 1, id.y, id.z))] - points[indexFromCoord(uint3(id.x + 1, id.y, id.z))];
    float dy = points[indexFromCoord(uint3(id.x, id.y - 1, id.z))] - points[indexFromCoord(uint3(id.x, id.y + 1, id.z))];
    float dz = points[indexFromCoord(uint3(id.x, id.y, id.z - 1))] - points[indexFromCoord(uint3(id.x, id.y, id.z + 1))];
    float3 gradient = { dx,dy,dz };
    gradient /= (chunkSize / (numPointsPerAxis - 1)) * 2;
    return -gradient;
}

[numthreads(numThreads, numThreads, numThreads)]
void March(int3 id : SV_DispatchThreadID)
{
    // Stop one point before the end because voxel includes neighbouring points
    //if (id.x >= numPointsPerAxis - 1 || id.y >= numPointsPerAxis - 1 || id.z >= numPointsPerAxis - 1) {
    //    return;
    //}
    int diff = 2;
    if (id.x >= numPointsPerAxis - diff || id.y >= numPointsPerAxis - diff || id.z >= numPointsPerAxis - diff  || id.x < 1 || id.y < 1 || id.z < 1) {
        return;
    }

    // 8 corners of the current cube
    uint3 targetIds[8] = {
        id + uint3(0,0,0),
        id + uint3(1,0,0),
        id + uint3(1,0,1),
        id + uint3(0,0,1),
        id + uint3(0,1,0),
        id + uint3(1,1,0),
        id + uint3(1,1,1),
        id + uint3(0,1,1)
    };

    float cubeCorners[8] = {
        points[indexFromCoord(targetIds[0])],
        points[indexFromCoord(targetIds[1])],
        points[indexFromCoord(targetIds[2])],
        points[indexFromCoord(targetIds[3])],
        points[indexFromCoord(targetIds[4])],
        points[indexFromCoord(targetIds[5])],
        points[indexFromCoord(targetIds[6])],
        points[indexFromCoord(targetIds[7])]
    };


    // Calculate unique index for each cube configuration.
    // There are 256 possible values
    // A value of 0 means cube is entirely inside surface; 255 entirely outside.
    // The value is used to look up the edge table, which indicates which edges of the cube are cut by the isosurface.
    int cubeIndex = 0;
    if (cubeCorners[0] < isoLevel) cubeIndex |= 1;
    if (cubeCorners[1] < isoLevel) cubeIndex |= 2;
    if (cubeCorners[2] < isoLevel) cubeIndex |= 4;
    if (cubeCorners[3] < isoLevel) cubeIndex |= 8;
    if (cubeCorners[4] < isoLevel) cubeIndex |= 16;
    if (cubeCorners[5] < isoLevel) cubeIndex |= 32;
    if (cubeCorners[6] < isoLevel) cubeIndex |= 64;
    if (cubeCorners[7] < isoLevel) cubeIndex |= 128;

    // Create triangles for current cube configuration
    for (int i = 0; triangulation[cubeIndex][i] != -1; i += 3) {
        // Get indices of corner points A and B for each of the three edges
        // of the cube that need to be joined to form the triangle.
        int a0 = cornerIndexAFromEdge[triangulation[cubeIndex][i]];
        int b0 = cornerIndexBFromEdge[triangulation[cubeIndex][i]];

        int a1 = cornerIndexAFromEdge[triangulation[cubeIndex][i + 1]];
        int b1 = cornerIndexBFromEdge[triangulation[cubeIndex][i + 1]];

        int a2 = cornerIndexAFromEdge[triangulation[cubeIndex][i + 2]];
        int b2 = cornerIndexBFromEdge[triangulation[cubeIndex][i + 2]];

        Triangle tri;
        tri.vertexA = lerp(realPos(targetIds[a0]), realPos(targetIds[b0]), getRatio(cubeCorners[a0], cubeCorners[b0]));
        tri.vertexB = lerp(realPos(targetIds[a1]), realPos(targetIds[b1]), getRatio(cubeCorners[a1], cubeCorners[b1]));
        tri.vertexC = lerp(realPos(targetIds[a2]), realPos(targetIds[b2]), getRatio(cubeCorners[a2], cubeCorners[b2]));
        //float3 normal = getNormal(tri.vertexA, tri.vertexB, tri.vertexC);
        //tri.normA = normal;
        //tri.normB = normal;
        //tri.normC = normal;
        //float3 normal = getNormal(tri.vertexA, tri.vertexB, tri.vertexC);
        tri.normA = lerp(getSdfGradient(targetIds[a0]), getSdfGradient(targetIds[b0]), getRatio(cubeCorners[a0], cubeCorners[b0]));
        tri.normB = lerp(getSdfGradient(targetIds[a1]), getSdfGradient(targetIds[b1]), getRatio(cubeCorners[a1], cubeCorners[b1]));
        tri.normC = lerp(getSdfGradient(targetIds[a2]), getSdfGradient(targetIds[b2]), getRatio(cubeCorners[a2], cubeCorners[b2]));
        tri.colorA = lerp(colors[indexFromCoord(targetIds[a0])], colors[indexFromCoord(targetIds[b0])], getRatio(cubeCorners[a0], cubeCorners[b0]));
        tri.colorB = lerp(colors[indexFromCoord(targetIds[a1])], colors[indexFromCoord(targetIds[b1])], getRatio(cubeCorners[a1], cubeCorners[b1]));
        tri.colorC = lerp(colors[indexFromCoord(targetIds[a2])], colors[indexFromCoord(targetIds[b2])], getRatio(cubeCorners[a2], cubeCorners[b2]));

        triangles.Append(tri);
    }
}


